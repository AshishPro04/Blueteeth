buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

// Manifest version information
def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    // TODO: Remove this dependency - Used for NonNull and Nullable annotations
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.jakewharton.timber:timber:3.1.0'
}

// Bintray-related
apply plugin: 'com.jfrog.bintray'

version = "${versionMajor}.${versionMinor}.${versionPatch}"
group = 'com.robotpajamas.blueteeth'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'blueteeth'
        desc = 'An Android Bluetooth library'
        websiteUrl = 'https://github.com/RobotPajamas/Blueteeth'
        vcsUrl = 'https://github.com/RobotPajamas/Blueteeth.git'
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true

        version {
            name = "${versionMajor}.${versionMinor}.${versionPatch}"
            released  = new Date()
            vcsTag = "${versionMajor}.${versionMinor}.${versionPatch}"
        }
    }
}
